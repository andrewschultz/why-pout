Version 1/240717 of Why Pout Tables by Andrew Schultz begins here.

"Tables for Why Pout"

volume the main tables

book very general stuff

table of main oronyms
w1 (text)	w2 (text)	posthom (text)	hom-txt-rule (rule)	think-cue	okflip	core	idid	best-room	check-rule	run-rule	wfull (topic)	think-advice (text)
"an"	"aim"	--	--	false	true	true	false	eh raw air aww	pre-an-aim rule	post-an-aim rule	--	--
"two"	"maps"	"too"	--	false	true	true	false	eh raw air aww	pre-two-maps rule	post-two-maps rule	--	--
"self"	"owns"	--	--	false	true	true	false	hype lane	pre-self-owns rule	post-self-owns rule	--	--
"pry"	"stalk"	--	--	false	true	true	false	hype lane	pre-pry-stalk rule	post-pry-stalk rule	--	--
"high"	"plain|plane"	--	--	false	true	true	false	hype lane	pre-high-plain rule	post-high-plain rule	--	"You can find a [b]HIGH PLAIN[r] [once-now of sco-pry-stalk] you have something that can reach that far up."
"nah|naah|naw"	"phase"	--	--	false	true	true	false	NaffHaze	vc-nah-phase rule	vr-nah-phase rule	--	--
"pole|pull"	"east"	--	--	false	true	true	false	NaffHaze	pre-pole-east rule	post-pole-east rule	--	--
"grow"	"star"	--	--	false	true	true	false	NaffHaze	pre-grow-star rule	post-grow-star rule	--	--
"shore"	"trail"	--	--	false	true	true	false	NaffHaze	pre-shore-trail rule	post-shore-trail rule	--	--
"key"	"pillar"	--	--	false	true	true	false	NaffHaze	pre-key-pillar rule	post-key-pillar rule	--	--
"known"	"ocean"	--	--	false	true	true	false	NoNotion	pre-known-ocean rule	post-known-ocean rule	--	--
"six"	"quid"	--	--	false	true	true	false	NoNotion	pre-six-quid rule	post-six-quid rule	--	--
"the"	"file"	--	--	false	true	true	false	nonotion	pre-the-file rule	post-the-file rule	--	"You may be able to make sense of [b]THE FILE[r] [here-in of nonotion] [once-now of whether or not number of unchatted followers is 0] you've gotten enough information from companions."
"war"	"file"	--	--	false	true	true	false	nonotion	pre-war-file rule	post-war-file rule	--	"You may be able to recover the [b]WAR FILE[r] on the wharf isle [here-in of nonotion] [once-now of sco-plan-tracker] you have something you can write down its main ideas in."
"grow"	"vial"	"vile"	--	false	true	true	false	nonotion	pre-grow-vial rule	post-grow-vial rule	--	--
"wan"	"dwarf"	--	--	false	true	true	false	wand wharf	pre-wan-dwarf rule	post-wan-dwarf rule	--	--
"wool"	"frock"	--	--	false	true	true	false	Wolf Rock	pre-wool-frock rule	post-wool-frock rule	--	--
"hide"	"out"	--	--	false	true	true	false	Wolf Rock	pre-hide-out rule	post-hide-out rule	--	--
"low"	"door"	--	--	false	true	true	false	Wolf Rock	pre-low-door rule	post-low-door rule	--	"You may be able to find a [b]LOW DOOR[r] [here-in of Wolf Rock] [once-now of sco-mice-tall] you have a companion or companions that are small enough to dig a bit better."
"well"	"own"	--	--	false	true	true	false	we loan	pre-well-own rule	post-well-own rule	--	--
"summer"	"chant"	--	--	false	true	true	false	we loan	pre-summer-chant rule	post-summer-chant rule	--	--
"bile"	"oh"	--	--	false	true	true	false	we loan	pre-bile-oh rule	post-bile-oh rule	--	"You may be able to expose some merchant's [b]BILE OH[r] [once-now of sco-summer-chant] you find something positive to say to prep yourself."
"whee"	"lone"	--	--	false	true	true	false	we loan	pre-whee-lone rule	post-whee-lone rule	--	"You may be able to enjoy your solitude with [b]WHEE LONE[r] [here-in of we loan] [once-now of sco-bile-oh] you actually have solitude."
"oh"	"flier|flyer"	"owe"	--	false	true	true	false	we loan	pre-oh-flyer rule	post-oh-flyer rule	--	"You may be able to say [b]OH FLIER[r] around the oaf liar [once-now of sco-six-quid] you have money."
"boost"	"role"	"roll"	--	false	true	true	false	hideout	pre-boost-role rule	post-boost-role rule	--	--
"low"	"bend"	--	--	false	true	true	false	lobe end	pre-low-bend rule	post-low-bend rule	--	--
"bay"	"sail|sale"	--	--	false	true	true	false	lobe end	pre-bay-sale rule	post-bay-sale rule	--	"You may be able to participate in a [b]BAY SALE[r] if you've found the right location and [once-now of sco-six-quid] you have money." [?? very detailed fix: we need to set a boolean somehow if this was guessed in Lobe End ]
"store"	"mile"	--	--	false	true	true	false	storm isle	pre-store-mile rule	post-store-mile rule	--	--
"mice"	"tall"	--	--	false	true	true	false	storm isle	pre-mice-tall rule	post-mice-tall rule	--	"You may be able to locate the [b]MICE TALL[r] [here-in of storm isle] [once-now of sco-gnome-old] you have a companion with good perception."
"mensch"	"elf"	--	--	false	true	true	false	storm isle	pre-mensch-elf rule	post-mensch-elf rule	--	--
"tree"	"small"	--	--	false	true	true	false	trees mall	pre-tree-small rule	post-tree-small rule	--	--
"tall"	"cake"	--	--	false	true	true	false	trees mall	pre-tall-cake rule	post-tall-cake rule	--	"You may be able to create a [b]TALL CAKE[r] [once-now of pre-tall-cake rule] you have ingredients and cooking instruments."
"treat"	"all"	"awl"	--	false	true	true	false	trees mall	pre-treat-all rule	post-treat-all rule	--	"You may be able to [b]TREAT ALL[r] [here-in of trees mall] [once-now of sco-tall-cake] you have a tasty treat."
"gnome"	"old"	--	--	false	true	true	false	keepiller	pre-gnome-old rule	post-gnome-old rule	--	"You may be able to recruit the [b]GNOME OLD[r] [here-in of keepiller] [once-now of sco-wool-frock] you have adventuring gear."
"rogue"	"old"	--	--	false	true	true	false	keepiller	pre-rogue-old rule	post-rogue-old rule	--	"You may be able to recruit the [b]ROGUE OLD[r] [here-in of keepiller] [once-now of sco-six-quid] you have money."
"manna"	"curb|kerb"	"mana"	vh-my-mana-curb rule	false	true	true	false	keepiller	pre-manna-curb rule	post-manna-curb rule	--	"You may be able to find a [b]MANNA CURB[r] beneath the manna curb [here-in of keepiller] [once-now of sco-mensch-elf] you have someone with knowledge of wildlife and wildflowers."
"plan"	"tracker"	--	--	false	true	true	false	keepiller	pre-plan-tracker rule	post-plan-tracker rule	--	--
"see"	"design"	"c|sea"	vh-c-sea-design rule	false	true	true	false	keep iller	pre-see-design rule	post-see-design rule	--	--
"knife"	"right"	"rite"	--	false	true	true	false	nigh fright	pre-knife-right rule	post-knife-right rule	--	--
"gaunt"	"raider"	--	--	false	true	true	false	worm eaten	pre-gaunt-raider rule	post-gaunt-raider rule	--	--
"were"	"meetin"	"whirr"	--	false	true	true	false	worm eaten	pre-were-meetin rule	post-were-meetin rule	--	--
"my"	"corps"	"core"	vh-my-core rule	false	true	true	false	worm eaten	pre-my-corps rule	post-my-corps rule	--	"You may be able to call your companions [b]MY CORPS[r] [once-now of sco-treat-all] [if pals-made < 2]you have companions[else]you've shared something together[end if]."
"cease"	"cull"	"seize"	hom-cease-cull rule	false	true	true	false	doom end	pre-cease-cull rule	post-cease-cull rule	--	--
"wipe"	"out"	--	--	false	true	true	false	doom end	pre-wipe-out rule	post-wipe-out rule	--	"You will want to [b]WIPE OUT[r] the cause of your problems [once-now of sco-cease-cull] you have them in your crosshairs."
"do|due"	"mend"	"dew"	--	false	true	true	false	doom end	pre-do-due-mend rule	post-do-due-mend rule	--	--

section air aww scoring

a wordtwisting rule (this is the pre-two-maps rule):
	if player is not in air aww or sco-an-aim is false, unavailable;
	ready;

this is the post-two-maps rule:
	now sco-two-maps is true;
	say "You look around for secrets. You find one line that is a hash of horizontal lines and another that is a hash of vertical lines. You put them together ... and you see a maze. You hope you can trace a passage out. And, with some effort, you do! Then you follow it. You hope it's right, and it leads somewhere better, or at least not as awful...";
	move player to Hype Lane;

section unsorted scoring

a wordtwisting rule (this is the pre-boost-role rule):
	if booze troll is not touchable, unavailable;
	if sco-boost-role is true:
		vcal "You already gave the booze troll a boost!";
		already-done;
	ready;

this is the post-boost-role rule:
	now sco-boost-role is true;
	say "You talk with the booze troll for a bit. They seem not to know who they are, and that gives you something in common with them. They don't want to be a lousy boozer all their life. They are sick of drinking Base Ale.[paragraph break]You begin chatting with them. You mention you've been having an adventure, and they'd sort of like one, too. They hand you a bottle of Base Ale as thanks. Not that they want you to go down their road. But it might be useful in bartering.";
	befriend booze troll;
	now player has Base Ale;

section eh raw air aww scoring

a wordtwisting rule (this is the pre-an-aim rule):
	if sco-an-aim is true:
		vcal "You already found a name and an aim!";
		already-done;
	ready;

this is the post-an-aim rule:
	now sco-an-aim is true;
	say "Yes. Now that you realize you want more than just to know your name, that you have bigger goals, you push a bit harder when you initially forget it. You remember patches of the past. You remember people harshly calling you by your last name, then your first.[paragraph break]Your name is Mike Orr. You even remember it's -- well, one of these two things: short for Michal, which everyone pronounced Michael until you just gave up, or ... well, Michael.[paragraph break]You look around a bit. You can see more, now. You're in some sort of tomb apse. You could exit--there are exits each way--but you suspect there's some horrible maze you could easily get lost in.";
	print-the-loc;

section NaffHaze scoring

a wordtwisting rule (this is the pre-pole-east rule):
	if player is not in naffhaze or sco-nah-phase is false, unavailable;
	if sco-pole-east is true:
		vcal "You already discovered how you were being policed!";
		already-done;
	ready;

this is the post-pole-east rule:
	now sco-pole-east is true;
	say "Ah, yes. It makes sense. You find the pole. Strangely, there's just a single switch to flip. And even more strangely, the switch you flip causes the pole to revert into the ground. Yay!";
	reveal Lobe End to east;
	check-worm-eaten;

a wordtwisting rule (this is the pre-shore-trail rule):
	if short rail is off-stage or player is not in NaffHaze, unavailable;
	if sco-shore-trail is true:
		vcal "But you already replaced the short rail!";
		already-done;
	ready;

this is the post-shore-trail rule:
	now sco-shore-trail is true;
	say "There must be more to the short rail than you see. And you start tracing where it is, where it can be, where it should go. You find something to dig up. It's ... surprisingly malleable. You have nothing else to do, anyway. Soon it feels like pulling cable from a ground. It's tough, since you can't see around, but indeed, the short rail does lead to a shore trail that leads south.";
	moot short rail;
	move shore trail to NaffHaze;
	reveal NoNotion to south;
	check-worm-eaten;

a wordtwisting rule (this is the pre-grow-star rule):
	if player is not in NaffHaze or gross tar is not in NaffHaze, unavailable;
	ready;

this is the post-grow-star rule:
	now sco-grow-star is true;
	say "You look at the gross tar, and you hope for a star in the sky to become brighter. It does, and clearly some of the tar is goopier than some of the other tar. You see a way to the north, and after you do, the tar isn't as relevant any more.";
	moot gross tar;
	reveal Wolf Rock to north;
	check-worm-eaten;

a wordtwisting rule (this is the pre-wipe-out rule):
	if sco-wipe-out is true:
		vcal "You already wiped out the sea skull! Now is the time for healing.";
		already-done;
	if pals-made is 0:
		vcp "You aren't feeling too great, but you don't want or need to wipe yourself out, yet. Perhaps you can find the root of your problems and wipe it out one day, though.";
		not-yet;
	if number of not friendly followers > 0:
		vcp "You don't want to wipe out the friendships you made.";
		not-yet;
	if player is not in doom end:
		vcp "You don't have anyone terribly evil you want to wipe out.";
		not-yet;
	if sco-cease-cull is false:
		vcp "You try to charge and wipe out the sea skull, but you bounce against an invisible barrier.";
		not-yet;
	ready;

this is the post-wipe-out rule:
	now sco-wipe-out is true;
	say "Well that does it! It's a big long fight, but you all prevail![paragraph break]Things are a bit of a mess, though. There is healing to do.";
	moot sea skull;

chapter Wolf Rock scoring

a wordtwisting rule (this is the pre-wool-frock rule):
	if player is not in wolf rock, unavailable;
	if sco-wool-frock is true:
		vcal "You already found one wool frock. You can't imagine you'd need two.";
		already-done;
	ready;

this is the post-wool-frock rule:
	now sco-wool-frock is true;
	say "You rummage about and find a wool frock.";
	now player has wool frock;

a wordtwisting rule (this is the pre-hide-out rule):
	if player is not in Wolf Rock, unavailable;
	if sco-hide-out is true:
		vcal "You already dispelled doubt and found the hideout!";
		already-done;
	ready;

this is the post-hide-out rule:
	now sco-hide-out is true;
	say "It's easy enough to say you should doubt yourself less. I mean, you'd feel stupid doubting it. But there's always a reason not to. Still, you involuntarily start a mantra: 'Fine, doubt? Find out!'[paragraph break]And what do you know? You find a hideout below!";
	reveal hideout to down;

a wordtwisting rule (this is the pre-low-door rule):
	if player is not in Wolf Rock, unavailable;
	if sco-mice-tall is false:
		vcp "Maybe there's a low door beneath the lode ore, but you don't have the ability to scrabble and scrape down through to find it[if pals-made > 0], and nobody around can help you[end if].";
		not-yet;
	if sco-low-door is true:
		vcal "You already found a low door in this mess!";
		already-done;
	ready;

this is the post-low-door rule:
	now sco-low-door is true;
	say "The lode and the ore are tough to shift around, but beneath one of the piles, you actually do find a low door, which you can go [b]INSIDE[r]. Hooray!";
	reveal We Loan to inside;

section lobe end scoring

a wordtwisting rule (this is the pre-low-bend rule):
	if player is not in lobe end, unavailable;
	if sco-low-bend is true:
		vcal "You already found a low bend!";
		already-done;
	ready;

this is the post-low-bend rule:
	now sco-low-bend is true;
	say "No, wait, there is a bit more to all this. If you work your way down a bit, well, there's a body of water. Which you can't cross at the moment, but maybe you'll figure a way.";

section lobe end scoring

a wordtwisting rule (this is the pre-bay-sale rule):
	if player does not have base ale, unavailable;
	if player is in NoNotion:
		vcp "[if sco-known-ocean is true]There's water here, but it's much more than a bay[else]You can't make out what sort of body of water is here. Well, not yet[end if].";
		not-yet;
	if player is not in lobe end, unavailable;
	if sco-six-quid is false:
		vcp "You realize that, in order to participate in a bay sale for a bay sail, you need some form of currency. You don't have that yet.";
		not-yet;
	ready;

this is the post-bay-sale rule:
	now sco-bay-sale is true;
	say "You have money, and you are ready for a sale, and you get it. Unfortunately, though you were expecting a nice big ship, you get something a little humbler, namely a We-Craft Weak Raft. It seems sturdy enough, though, and it does have a sail.[paragraph break]You christen it with the base ale, and the bottle fragments shatter and float away in the water.";
	quid-reduce 2;
	moot base ale;
	move We Craft Weak Raft to lobe end;
	reveal Storm Isle to north;

section storm isle scoring

a wordtwisting rule (this is the pre-store-mile rule):
	if player is not in storm isle, unavailable;
	if sco-store-mile is true:
		vcal "But you already transformed the storm isle!";
		already-done;
	ready;

this is the post-store-mile rule:
	now sco-store-mile is true;
	say "The storms let up. It's not such a big isle after all, but it does turn out to hold a whole lot of stores and vending areas. In particular, a stall labeled MY STALL, and a shelf labeled MEN SHELF.";
	move my stall to storm isle;
	move men shelf to storm isle;
	reveal Trees Mall to north;

a wordtwisting rule (this is the pre-mice-tall rule):
	if player is not in storm isle, unavailable;
	if sco-gnome-old is false:
		vcp "Maybe there are mice behind there, but you can't figure a way to locate them. It requires some sort of clever inhuman perception, you'd think.";
		not-yet;
	if sco-mice-tall is true:
		vcal "You already found the mice and destroyed the stall!";
		already-done;
	ready;

this is the post-mice-tall rule:
	now sco-mice-tall is true;
	say "You look into the stall and shrug your shoulders. Then the gnome (old) steps in and notes they hear something. Chittering noises! The gnome chitters back.[paragraph break]It takes a lot of negotiating, but you find where the mice are stuck. You and the gnome thud through and out come some mice, tall. They're tall for mice, though they really only come up above your knee.";
	befriend mice tall;
	moot my stall;

a wordtwisting rule (this is the pre-mensch-elf rule):
	if player is not in storm isle or men shelf is not in storm isle, unavailable;
	ready;

this is the post-mensch-elf rule:
	now sco-mensch-elf is true;
	say "You knock about the MEN shelf a bit, unable to find anything. It's really a collection of shelves that you disassemble and reassemble. Eventually, you're about to give up, and it all collapses, taking a chunk of wall with it. Our pops a mensch elf, quite grateful you helped them escape.";
	moot men shelf;
	befriend mensch elf;

section trees mall scoring

a wordtwisting rule (this is the pre-tree-small rule):
	if player is not in trees mall, unavailable;
	if sco-tree-small is true:
		vcal "You already got a tree (small) to grow!";
		already-done;
	ready;

this is the post-tree-small rule:
	now sco-tree-small is true;
	say "Amidst all the fake trees, an actual organic small tree grows. You're a bit disappointed, but you realize you maybe shouldn't have expected much with so little letter-shuffling. Maybe if you want a bigger tree, you'll need to do something else.";
	move tree small to Trees Mall;
	grow-the-tree;

a wordtwisting rule (this is the pre-tall-cake rule):
	if player does not have talk ache, unavailable;
	if player does not have manna and player has knife:
		vcp "You need ingredients to make a cake.";
		not-yet;
	if player has manna and player does not have knife:
		vcp "You need some sort of utensil to process the manna.";
		not-yet;
	if player does not have manna and player does not have knife:
		vcp "You have neither the utensils nor the ingredients to make a cake.";
		not-yet;
	ready;

this is the post-tall-cake rule:
	now sco-tall-cake is true;
	say "All this preparing food means you don't talk a bit, and as a bonus, your talk-ache is gone.[paragraph break]But now where to eat the cake?";
	moot talk ache;
	moot manna;
	now player has tall cake;

section trees mall scoring

a wordtwisting rule (this is the pre-treat-all rule):
	if player is not in trees mall, unavailable;
	if tree tall is not in trees mall, unavailable;
	if player does not have tall cake:
		vcp "You don't have something to treat your companions with!";
		not-yet;
	if sco-treat-all is true:
		vcal "You already shared treats!";
		already-done;
	ready;

this is the post-treat-all rule:
	now sco-treat-all is true;
	say "Well, that felt good. You trade more ideas while chatting. You think you have about everything figured out, now.";
	moot tall cake;

section hype lane scoring

a wordtwisting rule (this is the pre-self-owns rule):
	if player is not in hype lane, unavailable;
	if cell phones are not in hype lane, unavailable;
	ready;

this is the post-self-owns rule:
	now sco-self-owns is true;
	say "You take a step back from all the confident, constant chatter in the cell phones. You hear that, indeed, people are just repeating mantras, and they are not as smart as they think they are. You wonder if you should say something to the most egregious offenders ... and you eventually do! And they appear flustered. They attack you back for being too quiet. You mention that if they annoyed someone as too-quiet as you, they must have gone over the line.[paragraph break]Your confrontation did not go unnoticed by other cell phoners. They take the hint and moderate their voices.[paragraph break]However, now, a prize talk blasts through hidden loudspeakers. It exhorts you to find your dream, to find something that will help you move above and beyond everyone else. It's probably talking about money or power, but you're looking for something different.";
	move prize talk to hype lane;
	moot cell phones;

a wordtwisting rule (this is the pre-pry-stalk rule):
	if player is not in hype lane, unavailable;
	if prize talk is not in hype lane, unavailable;
	if sco-pry-stalk is true:
		vcal "You already pried a stalk!";
		already-done;
	ready;

this is the post-pry-stalk rule:
	now sco-pry-stalk is true;
	say "Among all the chatter, you manage to pry away a stalk from someone who is flattered you listen. It feels icky, but hey, the stalk looks like it can grow. But the question is, where can it grow to?";

a wordtwisting rule (this is the pre-high-plain rule):
	if player is not in hype lane, unavailable;
	if sco-pry-stalk is false:
		vcp "That would be a nicer place! But you have nothing that could help you move up.";
		not-yet;
	ready;

this is the post-high-plain rule:
	now sco-high-plain is true;
	say "You plant the stalk, which -- well, it's already a bit of a stalk, but it grows quickly and sturdily. It's not hard at all to climb. You are pretty sure you're aboveground, but it's hard to see for sure.";
	move player to NaffHaze;

section naff haze testing

a wordtwisting rule (this is the vc-nah-phase rule):
	if player is not in NaffHaze, unavailable;
	if sco-nah-phase is true:
		vcal "You already dispelled the haze!";
		already-done;
	ready;

this is the vr-nah-phase rule:
	now sco-nah-phase is true;
	say "The haze seemed too thick at first. But you recognize it doesn't have to be there. As you sift through it, you notice things that weren't there before. It lifts.";
	move short rail to NaffHaze;
	move gross tar to NaffHaze;
	move keep iller to NaffHaze;
	try looking; [pole east is implicitly moved that way]

a wordtwisting rule (this is the pre-key-pillar rule):
	if player is not in NaffHaze, unavailable;
	if sco-nah-phase is false, unavailable;
	if sco-key-pillar is true:
		vcal "You already opened the keep (iller)!";
		already-done;
	ready;

this is the post-key-pillar rule:
	now sco-key-pillar is true;
	say "You look around the pillars for a key, since there's no flowerpot to hide it under. You find one, and you open the way west.";
	reveal KeepIller to west;
	check-worm-eaten;

to check-worm-eaten:
	if plane-dir-score is 4:
		say "[line break]You've changed the landscape a lot ... enough that you hear a rumbling that has created a passage beneath.";
		reveal Worm Eaten to down;

section no notion scoring

a wordtwisting rule (this is the pre-known-ocean rule):
	if player is not in NoNotion, unavailable;
	if sco-known-ocean is true:
		vcal "But you already got a notion for the ocean. And you didn't even need a potion!";
		already-done;
	ready;

this is the post-known-ocean rule:
	now sco-known-ocean is true;
	say "Ah, yes. Things clear up a bit now. There we go ... you see where an ocean should be, and ... yes, you dip your toe in some wetness. Things clear up. You even see a sick squid! Maybe you can help it.";
	move sick squid to NoNotion;

a wordtwisting rule (this is the pre-six-quid rule):
	if player is not in nonotion, unavailable;
	if sco-six-quid is true:
		vcal "You don't need any more money, and the squid doesn't need any more help.";
		already-done;
	ready;

this is the post-six-quid rule:
	now sco-six-quid is true;
	say "You have an idea what is bothering the squid. Yes, a few coins stuck. Surprisingly, it lets you help out. 'Current, sea, currency,' you mutter as you poke around. Ah, there you go.[paragraph break]You offer to give it some of the coins it almost choked on, but it does not need them. It seems to wait around, as if to help you with travel to places you may get to go. It's willing to wait.[paragraph break]";
	now current-quid is 6;

[to check-squid-flyer:
	if player has maps:
		say "[line break]The squid gestures towards the two maps, as if it can take you wherever you need to on the maps. But it does look a little exhausted. So you don't just want to summon it for a joyride.";]

a wordtwisting rule (this is the three-isle rule):
	if player is in lobe end and sco-low-bend is true and player has flier:
		vcp "No, this can't be the right place.";
		not-yet;
	if player is not in nonotion, unavailable;
	if player does not have flier, unavailable;

a wordtwisting rule (this is the pre-the-file rule):
	abide by the three-isle rule;
	if number of unchatted followers > 0:
		vcp "You know there is THE FILE, but which file is THE FILE? The thief isle has probably stolen a lot! You need information from your companions[if number of not friendly followers > 0], or maybe just more companions[end if].";
		not-yet;
	if sco-the-file is true:
		vcal "You already visited thief isle!";
		already-done;
	ready;

this is the post-the-file rule:
	now sco-the-file is true;
	say "You have a clue what isle to look through. The thief isle. You get The File.";
	conditional-flier-mangle;

to conditional-flier-mangle:
	if flier-isle-score is 3:
		say "[line break]You've visited all the locations in the flier. You crumple it up as if to throw it in a trash bin. The squid makes a gesture as if it wishes to gobble the flier. You're worried it might choke on the flier, as it did for the quid, but then -- well, the water washes the paper brochure down. Odd, but hooray for clearer inventory.";
		moot flier;

a wordtwisting rule (this is the pre-war-file rule):
	abide by the three-isle rule;
	if sco-plan-tracker is false:
		vcp "A war file might be useful, but you'd have no way to organize the information right now.";
		not-yet;
	if sco-war-file is true:
		vcal "You already got the war file!";
		already-done;
	ready;

this is the post-war-file rule:
	now sco-war-file is true;
	say "The squid gives you[if number of friendly followers > 0] and only you[end if] a ride a few miles across the ocean. You realize you could reach the wharf isle for a war file. When you get there, you're a bit disappointed by what you see. There's a bit too much emphasis on magic and the implements thereof, all of which are at a much too hefty price for you. So equipment is out. Still, there's some ideas worth tracking. Yet you can't be the only person disappointed here...";
	now block-followers is true;
	move player to Wand Wharf;
	ride-squid;

to ride-squid:
	if player is in Wand Wharf:
		if pals-made >= 1:
			say "There was more than enough room on the squid for [if pals-made is 1][the random friendly follower][else if pals-made is 2]both your companions[else]all your companions[end if], too.";
	follow-you;

a wordtwisting rule (this is the pre-grow-vial rule):
	abide by the three-isle rule;
	if sco-grow-vial is true:
		vcal "You already got a grow-vial!";
		already-done;
	ready;

this is the post-grow-vial rule:
	now sco-grow-vial is true;
	say "In the grove isle, you find a grow vial, which you take.";
	now player has grow vial;
	conditional-flier-mangle;

chapter wand wharf scoring

a wordtwisting rule (this is the pre-wan-dwarf rule):
	if player is not in wand wharf, unavailable;
	ready;

this is the post-wan-dwarf rule:
	now sco-wan-dwarf is true;
	say "You look around and uncover a wan dwarf who wasn't good enough to be a full fighter but rejected for magic training. You explain your situation. They're all in![paragraph break]There's not much left to do here, so you ride the squid back.";
	befriend wan dwarf;
	move player to NoNotion;
	now block-followers is false;
	conditional-flier-mangle;

chapter we loan scoring

a wordtwisting rule (this is the pre-well-own rule):
	if player is not in we loan, unavailable;
	if sco-well-own is true:
		vcal "You already exposed the shady side here.";
		already-done;
	ready;

this is the post-well-own rule:
	now sco-well-own is true;
	say "Yes. There's a definite shady side. You mumble to yourself, and then your mumbles get louder, and it attracts ... some merchant. They begin babbling about deals they have, fake-friendly, about how you can buy low.";
	move some merchant to We Loan;

a wordtwisting rule (this is the pre-summer-chant rule):
	if player is not in we loan or merchant is not in we loan, unavailable;
	if sco-summer-chant is true:
		vcal "You already found cheeriness outside of the merchant!";
		already-done;
	ready;

this is the post-summer-chant rule:
	now sco-summer-chant is true;
	say "You think up a summer chant you mumble under your breath. The merchant finds it a bit weird you like THAT as a way to keep positive. It doesn't seem like that sort of thing helps the economy. Their cheeriness slips slightly, with impressively-balanced potshots at people more and less successful than they are.";

a wordtwisting rule (this is the pre-bile-oh rule):
	if player is not in we loan or merchant is not in we loan, unavailable;
	if sco-summer-chant is false:
		vcp "You need to find something positive to say, first. It doesn't have to be particularly rigorous, but it needs to be positive.";
		not-yet;
	ready;

this is the post-bile-oh rule:
	now sco-bile-oh is true;
	say "The mere suggestion that the merchant's aggression may be annoying seems to set them off. No, no, it's people like YOU who are the problem. Without dynamic positivity like theirs, you're totally screwed! You're just jealous you don't have it![paragraph break]You freeze up, confused by all this, and the merchant takes this as a sign of arrogant disrespect. You are not the first person to reject their positivity! They run into clueless crumb-bums all day![paragraph break]They storm off, presumably to find another crumb-bum to abuse for not having their brand of positivity.[paragraph break]Oh, hey, you can maybe enjoy some solitude now!";
	moot merchant;

a wordtwisting rule (this is the pre-whee-lone rule):
	if player is not in we loan, unavailable;
	if sco-well-own is false:
		vcp "It's nice to be alone here. But you sense there's a challenge here, to earn solitude, first.";
		not-yet;
	if merchant is in we loan:
		vcp "You can't enjoy solitude with that merchant around.";
		not-yet;
	if sco-whee-lone is true:
		vcal "You already got this place to yourself!";
		already-done;
	ready;

this is the post-whee-lone rule:
	now sco-whee-lone is true;
	say "You enjoy your solitude, but ... wait. You lose track of time, and an oaf liar slips in just to babble at you.";
	move oaf liar to we loan;

a wordtwisting rule (this is the pre-oh-flyer rule):
	if player is not in we loan or oaf liar is not in we loan, unavailable;
	if sco-six-quid is false:
		vcp "[one of]You notice a flyer behind their back, and you nudge them as if to say, can I have it for free? No, no, you cannot[or]You still haven't found any way to pay for the flyer[stopping].";
		not-yet;
	ready;

this is the post-oh-flyer rule:
	now sco-oh-flyer is true;
	say "You act distracted just right. The oaf liar tries to get your attention. Just ignoring them wouldn't work, but paying attention to a flyer ... well, they take the hint that you aren't a high-rolling customer.";
	moot oaf liar;
	now player has flier;
	quid-reduce 1;

section keepiller scoring

a wordtwisting rule (this is the pre-gnome-old rule):
	if player is not in keepiller, unavailable;
	if sco-wool-frock is false:
		vcp "You summon a gnome, old, to compliment them on keeping the keep looking so nice all these years. They thank you but mention they're a bit sick of it and they seek adventure. Yet they can't go adventuring in these tatty clothes.";
		not-yet;
	if sco-gnome-old is true:
		vcal "You already summoned the gnome!";
		already-done;
	ready;

this is the post-gnome-old rule:
	now sco-gnome-old is true;
	say "A gnome, old, arrives from a room above in the keep. They offer to adventure with you, if you have suitable enough adventuring gear. As a matter of fact, you do. The wool frock works well.[paragraph break]'Less gnome attic, more nomadic,' they muse.";
	befriend gnome old;
	moot wool frock; [now gnome has wool frock?]

a wordtwisting rule (this is the pre-rogue-old rule):
	if player is not in keepiller, unavailable;
	if sco-six-quid is false:
		vcp "A rogue, old, appears, but after some conversation, you realize you don't have any money to pay them with. They'd like to help, but they worked for free once and felt ripped off. Maybe later.";
		not-yet;
	if sco-rogue-old is true:
		vcal "But you already got the rogue (old) to join you!";
		already-done;
	ready;

this is the post-rogue-old rule:
	now sco-rogue-old is true;
	say "A rogue, old, arrives. They mention they'd like to help you but, you know, the row (gold) needs maintenance. You offer to pay them three quid..";
	befriend rogue old;
	quid-reduce 3;

to quid-reduce (nu - a number):
	say "[line break]This transaction reduces your wealth from [current-quid in words] quid to [(current-quid - nu) in words] quid.";
	decrease current-quid by nu;
	if current-quid is 0:
		say "[line break]Well, easy come, easy go. You're pretty sure you've visited everywhere and found everyone who might need money.";

a wordtwisting rule (this is the pre-manna-curb rule):
	if player is not in keepiller, unavailable;
	if sco-mensch-elf is false:
		vcp "Perhaps there is a lot of good stuff beneath the manic herb, but you don't have the specialized knowledge to figure it out.";
		not-yet;
	if sco-manna-curb is true:
		vcal "You already found enough manna beneath the manic herb!";
		already-done;
	ready;

this is the vh-my-mana-curb rule:
	say "You do not have any magic points, so you don't need them refreshed. But you must be close!"

this is the post-manna-curb rule:
	now sco-manna-curb is true;
	say "You suspect there is manna beneath the manic herb, but you have no clue how to find it. Thankfully, the mensch elf steps up. You wind up with a lot of manna, whatever that is!";
	now player has manna;

a wordtwisting rule (this is the pre-plan-tracker rule):
	if player is not in keepiller or plant racker is not touchable, unavailable;
	ready;

this is the post-plan-tracker rule:
	now sco-plan-tracker is true;
	say "Wow! That was ... surprisingly simple. You find a rather new-looking plan tracker behind the plant racker, which crumbles to dust as you push it aside.";
	moot plant racker;
	now player has plan tracker;

this is the vh-c-sea-design rule:
	say "Close, but an action is needed here."

a wordtwisting rule (this is the pre-see-design rule):
	if player is not in keepiller, unavailable;
	if pals-made < 2:
		vcp "You sort of want to see the design, but you're sort of scared what will happen once you do. You feel like you need some [if pals-made is 1]more [end if]support before really trying.";
		not-yet;
	if sco-see-design is true:
		vcal "You already saw the design in the seedy sign. You don't need to redo things.";
		already-done;
	ready;

this is the post-see-design rule:
	now sco-see-design is true;
	now block-followers is true;
	say "You see the design in the seedy sign. It leads somewhere scary. Somewhere you don't want your friends to visit. Though you appreciate their support. You'll be right back. You think. You hope.";
	move player to Nigh Fright;

section nigh fright scoring

a wordtwisting rule (this is the pre-knife-right rule):
	if player is not in nigh fright, unavailable;
	ready;

this is the post-knife-right rule:
	now sco-knife-right is true;
	say "With your friends boosting your spirits, you look for a sacred/profane weapon. It turns up. Eerie howling begins. You hightail it out of there.";
	now player has knife right;
	drop-player-at KeepIller;

section worm eaten scoring

a wordtwisting rule (this is the pre-gaunt-raider rule):
	if player is not in worm eaten or gone traitor is not in worm eaten, unavailable;
	ready;

this is the post-gaunt-raider rule:
	now sco-gaunt-raider is true;
	say "After some discussion, you work out a plan to make the gone trader reframe what they did. Whoever they were, the evil powers, they rejected them instead of doubling down. Perhaps there is a way to get back at them. They know things you never could hope to.";
	moot gone traitor;
	befriend gaunt raider;

a wordtwisting rule (this is the pre-were-meetin rule):
	if player is not in worm eaten, unavailable;
	if sco-were-meetin is true:
		vcal "You already held a meeting! Too many meetings get in the way of doing.";
		already-done;
	abide by the followers-check rule;

a wordtwisting rule (this is the followers-check rule):
	if pals-made is 0:
		vcp "But you have no friends to meet with!";
		not-yet;
	if number of not friendly followers > 0:
		vcp "You sense you don't have the gang together! [if number of not friendly followers is 1]But you must be very, very close[else]You may still be a way away[end if].";
		not-yet;

this is the post-were-meetin rule:
	now sco-were-meetin is true;
	say "You meet with the friends you've made over your adventure. You all have one goal in mind. And yet you don't have a rallying cry. Perhaps it can come from within you.";

section worm eaten scoring

a wordtwisting rule (this is the pre-my-corps rule):
	if sco-an-aim is false, unavailable;
	abide by the followers-check rule;
	if player is not in worm eaten:
		vcp "This doesn't seem like the right place.";
		not-yet;
	if sco-were-meetin is false:
		vcp "You need to bring everyone together fully first.";
		not-yet;
	if player is not in worm eaten:
		vcp "This doesn't seem to be the right place to pep up the troops.";
		not-yet;
	if sco-treat-all is false:
		vcp "They are your corps, and yet, there's something to share with them. Something to do together, first.";
		not-yet;
	if sco-my-corps is true:
		vcal "You already brought your corps a bit closer together!";
		already-done;
	ready;

this is the vh-my-core rule:
	say "You always want to strengthen your own core, but you have a feeling you're looking too inward, here. Though you must be close."

this is the post-my-corps rule:
	now sco-my-corps is true;
	say "Your pep talk brings everyone together. They are ready for ... well, whatever is next. This feels like the right place. You clear out a passage below. You swear a solemn pact that everyone will defend everyone else, regardless of what happens.";
	now Doom End is mapped below Worm Eaten;

chapter doom end scoring

a wordtwisting rule (this is the pre-cease-cull rule):
	if player is not in doom end or sea skull is not in doom end, unavailable;
	if sco-cease-cull is true:
		vcal "You already played defense. Now's the time to go on offense!";
		already-done;
	ready;

this is the hom-cease-cull rule:
	say "No, seizing is the last thing you want the skull to do. And you aren't foolish enough to want to seize power."

this is the post-cease-cull rule:
	now sco-cease-cull is true;
	say "You'll probably have to alert the sea skull to your presence at some time. You try to sound authoritative when you do so. And ... well, you get its attention. It begins pouring out rivers of blood at you and your companions. But fortunately, you have so many, that by the time it gets to you, it is not so bad. Yet it's still depressing. You think back to the advice you got at the beginning ... how it seemed slightly off. What to do?";

a wordtwisting rule (this is the pre-do-due-mend rule):
	if player is not in doom end, unavailable;
	ready;

this is the post-do-due-mend rule:
	now sco-do-due-mend is true;
	say "Yes. It's time to get healing. You're not quite sure how, but at least you're sure of who you are. And your companions are, too.";
	follow the score and thinking changes rule;
	end the story finally saying "Gray day?! Grade A!";
	follow the shutdown rules;

volume command parsing

the check forks rule is listed first in the for printing a parser error rulebook.

rule for printing a parser error (this is the check forks rule):
	now partial-row is 0;
	now got-partial-done is false;
	abide by the main-wordtwisting-checker rule for table of main oronyms;
	[if mrlp is valid, abide by the main-wordtwisting-checker rule for spoontable of mrlp;]
	if partial-row > 0:
		say "Hmm. You are on the right track, there.";
		the rule succeeds;
	if got-partial-done is true:
		say "Hmm, no, you already did that, or something like that. You'll know if and when you need to flip between things.";
		the rule succeeds;
	continue the action;

rule for printing a parser error when the latest parser error is the not a verb I recognise error (this is the catch bad verbs rule):
	if core-score is 0:
		say "You're stuck with what to do right now. Perhaps you can [if gs-examined-self is true]look inward again with X ME[else]look inward[end if], to give you an idea of what you want. You can't exactly look outward.";
		increment first-point-clue-flag;
		if gs-parser-error-note is false:
			say "[line break][i][bracket][b]NOTE[r][i]: until the main area, parser verb errors like this will give occasional hints. So don't be afraid to try stuff.[close bracket][r][line break]";
			now gs-parser-error-note is true;
			the rule succeeds;
		increment counter-zero-points;
		if counter-zero-points > number of rows in table of zero point taunts:
			now counter-zero-points is 1;
		if first-point-clue-flag is 4:
			now first-point-clue-flag is 0;
			say "[line break][one of]A cruel voice mocks [or]That cruel voice, again. [stopping][one of]'Way to wait, ooh!'[or]'You'd be better off [b]WAIT[r]ing or typing nothing.'[or][random-taunt][stopping]";
	else if core-score is 1:
		say "You need to find something that will get you out of the tomb apse. You remembered a name (yours) by finding an aim. What now?";
	else:
		say "Trying that action brings up nothing. [this-game] has a limited set of verbs, and the main thing is to guess a two-word phrase. To see what verbs are used, try [b]VERBS[r].";
	the rule succeeds;

counter-zero-points is a number that varies.

to say random-taunt:
	choose row counter-zero-points in table of zero point taunts;
	say "[taunt-text entry][if counter-zero-points is number of rows in table of zero point taunts] (that's the last of the specific hints.)";

table of zero point taunts
sortval	taunt-text
-2	"'Way to wait, ooh!'"
-1	"'Nothing you've tried has worked. You'd be better off [b]WAIT[r]ing or typing nothing.'"
0	"'Bro, clues? Broke! Lose!'"
0	"'Boost-op? Boo! Stop!'"
0	"'Duh, DO?! Dud, ooh!'"
0	"'Play some place, umm...'"
0	"'Find a fine day,' just like your original captors."
1	"'Take a look where you are.'"

rule for printing a parser error when the latest parser error is the can't see any such thing error:
	say "Nothing here like that[one of]. Note that room names are not usually worth examining, as you can just [b]L[r] or [b]LOOK[r][or][stopping]."

volume can't go that way

table of noways
noway-rm	noway-txt
Eh Raw Air Aww	"[if sco-an-aim is false]You can't see anywhere. You know almost nothing. I mean, you don't even have a name. What would you do even if there was a passage?[else if number of viable directions is 0]You still can't find anywhere to go, yet. But there seem to be clues where to go, and how, if you look around.[else]You haven't made passage [noun] yet, though you can go [list of viable directions].[end if]"

volume scoring to move [see RoL Defs if I add bonus points]

the score and thinking changes rule is listed instead of the notify score changes rule in the turn sequence rulebook.

to say went-by (nu - a number): say "just went [if nu > 0]up[else]down[end if] by [nu in words] point[unless nu is 1 or nu is -1]s[end if]";

this is the score and thinking changes rule:
	let sco-delt be current-score - last-current-score;
	if sco-delt is 0, continue the action;
	say "[i][bracket]Your score just went up by [if sco-delt is 1]a point[else][sco-delt in words] points[end if]![close bracket][r][line break]";
	now last-current-score is current-score;

Why Pout Tables ends here.

---- DOCUMENTATION ----
